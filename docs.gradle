/*
 * Copyright 2010-2011 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import org.apache.tools.ant.filters.ReplaceTokens

/**
 * @author Andres Almiray, Jan Novotny, Marcel Matula, Michal Bernhard
 */
buildscript {
    repositories {
        mavenCentral()
        mavenRepo name: 'SpringSource', urls: 'http://repository.springsource.com/maven/bundles/release'
    }

    dependencies {
        classpath("org.grails:grails-docs:1.3.6") { transitive = false }
        classpath("org.xhtmlrenderer:core-renderer:R8pre2") { transitive = false }
        classpath("com.lowagie:itext:2.0.8") { transitive = false }
        classpath("radeox:radeox:1.0-b2") { transitive = false }
    }
}

task copyDocs(type: Copy) {
    destinationDir = "${buildDir}/javadoc-src" as File

    from('src/main/groovy/groovyx/gpars') {
        include '**/*'
        include '**/*.html'
    }
}

task buildDocs(type: Groovydoc) {
    dependsOn { copyDocs }

    classpath = configurations.docs
    source = files("$buildDir/javadoc-src")
    destinationDir = "$buildDir/manual/api" as File
    docTitle = "Griffon $version"
    header = "Griffon $version"
    windowTitle = "Griffon $version"
    groovyClassPath = configurations.docs
    includePrivate = true
    use = true

    link('http://java.sun.com/j2se/1.5.0/docs/api', 'java.,org.xml.,javax.,org.xml.')
    link('http://www.dpml.net/api/ant/1.7.0', 'org.apache.ant.,org.apache.tools.ant.')
    link('http://junit.sourceforge.net/junit3.8.1/javadoc/', 'org.junit.,junit.framework.')
    link('http://groovy.codehaus.org/api/', 'groovy.,org.codehaus.groovy.')
}

task jarApi(type: Jar) {
    dependsOn { buildDocs }

    classifier = 'javadoc'
    destinationDir = "$buildDir/assemble/jars" as File
    from "$buildDir/manual/api"
}

task jarSource(type: Jar) {
    dependsOn { copyDocs }

    classifier = 'sources'
    destinationDir = "$buildDir/assemble/jars" as File
    from "$buildDir/javadoc-src"
    exclude "**/*.html"
}

task jarDocs(dependsOn: [jarApi, jarSource]) {}

task prepareGuide(type: Copy) {
    from('grails-doc/src')
    into("$buildDir/manual-src")
}

task buildGuide(type: Copy) {
    dependsOn { prepareGuide }

    def manualSrc = "$buildDir/manual-src" as File
    destinationDir = "$buildDir/manual" as File
    from(manualSrc) {
        include '*.properties'
    }

    doLast {
        ant.taskdef(name: 'docs', classname: 'grails.doc.ant.DocPublisherTask', classpath: configurations.docs.asPath);
        ant.docs(src: "$buildDir/manual-src",
                dest: "$buildDir/manual",
                properties: "grails-doc/src/doc.properties",
                styleDir: new File('grails-doc/resources/style'),
                cssDir: new File('grails-doc/resources/css'),
                imagesDir: new File('grails-doc/resources/img')
        )
    }

}

task pdfGuide(type: Copy) {
    dependsOn { buildGuide }

    inputs.dir("$buildDir/manual" as File)
    outputs.files "$buildDir/manual/guide.pdf"

    doFirst {
        try {
            grails.doc.PdfBuilder.build(
                    basedir: buildDir.absolutePath,
                    home: project.file('.').absolutePath,
                    tool: 'gpars'
            )
        } catch (x) {
            println(x)
        }
    }

    destinationDir = "$buildDir/manual" as File
    from "$destinationDir/guide/"
    include '*.pdf'
    rename 'single.pdf', "guide.pdf"

    doLast {
        delete "$destinationDir/guide/single.pdf"
    }
}

task zipGuide(type: Zip) {
    dependsOn { [pdfGuide, jarDocs] }
    appendix = 'guide'
    from "$buildDir/manual"
}
